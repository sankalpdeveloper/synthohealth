archetype (adl_version=1.4; uid=f2c50f1f-3530-4b70-a0bf-7c11948652c7)
	openEHR-EHR-OBSERVATION.aedes_indices.v0

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"2023-07-17">
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
	>
	lifecycle_state = <"in_development">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the parameters used to monitor the population density and breeding rate of Aedes mosquitoes.">
			keywords = <"aedes","mosquito","dengue","ovitrap">
			copyright = <"Â© openEHR Foundation">
			use = <"Use to record parameters used to monitor the population density and breeding rate of Aedes mosquitoes, as vectors for Dengue fever, Zika virus, Chikungunya and Yellow fever.">
		>
	>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"129AE762615BC4E596291E5C790E4590">
		["build_uid"] = <"f0a7ff32-f2be-48ad-8ca3-75a20284b097">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Aedes indices
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {    -- Location category
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {    -- House index (HI)
										value matches {
											DV_PROPORTION matches {
												type matches {2}
											}
										}
									}
									ELEMENT[at0008] occurrences matches {0..1} matches {    -- Container index (CI)
										value matches {
											DV_PROPORTION matches {
												type matches {2}
											}
										}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {    -- Breteau index (BI)
										value matches {
											DV_PROPORTION matches {
												denominator matches {|100.0|; 100.0}
												type matches {0}
											}
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {    -- Ovitrap index (OI)
										value matches {
											DV_PROPORTION matches {
												type matches {2}
											}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0005] matches {    -- ITEM_TREE
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0010] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Aedes indices">
					description = <"Public health parameters used to monitor the population density and breeding rate of Aedes mosquitoes.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Location category">
					description = <"The category or type of location being monitored.">
					comment = <"For example: home, school, or workplace.">
				>
				["at0006"] = <
					text = <"Breteau index (BI)">
					description = <"The number of positive containers per 100 houses.">
					comment = <"Calculated as (number of positive containers / number of houses inspected) * 100.">
				>
				["at0007"] = <
					text = <"House index (HI)">
					description = <"The percentage of houses or premises infested with larvae and/or pupae.">
					comment = <"Calculated as (number of positive containers / number of houses inspected) * 100.">
				>
				["at0008"] = <
					text = <"Container index (CI)">
					description = <"The percentage of water-holding containers infested with larvae or pupae.">
					comment = <"Calculated as (number of positive containers / total number of containers inspected) * 100.">
				>
				["at0009"] = <
					text = <"Ovitrap index (OI)">
					description = <"The percentage of ovitrap devices that contain eggs or larvae.">
				>
				["at0010"] = <
					text = <"Extension">
					description = <"">
				>
				["at0005"] = <
					text = <"ITEM_TREE">
					description = <"">
				>
			>
		>
	>
