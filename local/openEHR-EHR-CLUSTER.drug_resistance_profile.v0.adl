archetype (adl_version=1.4; uid=fb26ff5b-b5a8-420d-b058-c351ec68ecd6)
	openEHR-EHR-CLUSTER.drug_resistance_profile.v0

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"2022-10-26">
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
	>
	lifecycle_state = <"in_development">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record information about the reduction in the effectiveness of a medication in treating a condition.">
			keywords = <"medication","drug">
			copyright = <"Â© openEHR Foundation">
			use = <"Use to record information about the reduction in the effectiveness of a medication in treating a condition.

This archetype has been designed for nesting within:
- the 'Analyte result detail' SLOT within the CLUSTER.laboratory_test_analyte archetype to record the drug resistance profile of a single microorganism;
- the 'Status' SLOT in the EVALUATION.problem_diagnosis archetype, to record the drug resistance profile of a diagnosed condition; or
- other clinically relevant archetypes.">
		>
	>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"C201259A16CB389E1420236A2DD3475E">
		["build_uid"] = <"67ea2908-9a18-45a5-ba9d-a79907d98ecd">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {    -- Drug resistance profile
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {    -- Index condition
				value matches {
					DV_TEXT matches {*}
				}
				name matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0003,    -- Organism
							at0004]    -- Disease
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {    -- Resistance category
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0011] occurrences matches {0..1} matches {    -- Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0010] occurrences matches {0..1} matches {    -- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0005] occurrences matches {0..*} matches {    -- Specific resistance per drug
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0006] matches {    -- Drug name
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0007] occurrences matches {0..1} matches {    -- Resistant?
						value matches {
							DV_TEXT matches {*}
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0008,    -- Yes
									at0009]    -- No
								}
							}
							DV_BOOLEAN matches {*}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Drug resistance profile">
					description = <"Information about the reduction in the effectiveness of a medication in treating a condition.">
					comment = <"For example: an antibiotic to treat an infection or chemotherapy to treat a cancer.">
				>
				["at0001"] = <
					text = <"Index condition">
					description = <"Identification of the disease or causal microbe.">
					comment = <"For example: 'Tuberculosis' or 'Mycobacterium tuberculosis'. This data element may be redundant if the disease or causal microbe is identified in the parent archetype.">
				>
				["at0002"] = <
					text = <"Resistance category">
					description = <"Category of drug resistance observed in relation to the 'Index condition'.">
					comment = <"For example: 'No resistance', 'Monoresistant', 'Polydrug resistant', 'Multi-drug resistant (MDR), or 'Extensively drug resistant' to Tuberculosis.">
				>
				["at0003"] = <
					text = <"Organism">
					description = <"Name of the causal organism for an infectious disease.">
				>
				["at0004"] = <
					text = <"Disease">
					description = <"Name of the disease.">
				>
				["at0005"] = <
					text = <"Specific resistance per drug">
					description = <"Details about the resistance of the condition or microbe to an identified drug.">
				>
				["at0006"] = <
					text = <"Drug name">
					description = <"Name of the medication or drug.">
				>
				["at0007"] = <
					text = <"Resistant?">
					description = <"The organism is resistant to the identified drug?">
				>
				["at0008"] = <
					text = <"Yes">
					description = <"">
				>
				["at0009"] = <
					text = <"No">
					description = <"">
				>
				["at0010"] = <
					text = <"Comment">
					description = <"Narrative description about the drug resistance profile for the identified condition or microbe, not captured in other fields.">
				>
				["at0011"] = <
					text = <"Description">
					description = <"Narrative description about the drug resistance profile for the identified disease or causal microbe.">
				>
			>
		>
	>
